package rules;

import com.example.SBNZ.model.training.InputDataTraining;
import com.example.SBNZ.model.training.Training;
import com.example.SBNZ.model.training.Exercise;
import com.example.SBNZ.model.training.Session;
import com.example.SBNZ.model.training.CurrentFact;

import com.example.SBNZ.enums.diet.Goal;
import com.example.SBNZ.enums.training.Difficulty;
import com.example.SBNZ.enums.training.ExerciseCategory;
import com.example.SBNZ.enums.training.Muscle;

rule "When goal is Mass gain, difficulty and using equipment are given-> get adequate exercises"
	no-loop
	when
	    $exercises: List(size > 0) from collect(Exercise())
	    $currentFact: CurrentFact(goal == Goal.MASS_GAIN, daysWeek == -1)
        $inputData: InputDataTraining($difficulty: difficulty,
                                      $equipment: equipment,
                                      $injuries: injuries)

        accumulate(
                   Exercise($e: this,
                            $muscleList: muscleList,
                            difficulty == $difficulty,
                            equipment == $equipment,
                            exerciseCategory == ExerciseCategory.MASS_GAIN || exerciseCategory == ExerciseCategory.ALL
                            ) from $exercises and
                            Number(intValue == 0) from accumulate(
                                Muscle($m: this, $muscleList.contains(this)// this memberOf $muscleList
                                ) from $injuries,
                                count(1)
                            ),
                   $filteredExercises: collectList($e)
        )
	then
	    System.out.println("[TRAINING] Second layer - goal is mass gain, filtering the list of exercises and setting daysweek and rest time to 0");
        $currentFact.setExerciseList($filteredExercises);
        $currentFact.setDaysWeek(0);
        $currentFact.setRestTime(0);
        update($currentFact);
	end

rule "When goal is Weight loss, difficulty and using equipment are given-> get adequate exercises"
	no-loop
	when
	    $exercises: List(size > 0) from collect(Exercise())
	    $currentFact: CurrentFact(goal == Goal.WEIGHT_LOSS, daysWeek == -1)
        $inputData: InputDataTraining($difficulty: difficulty,
                                      $equipment: equipment,
                                      $injuries: injuries)

        accumulate(
                   Exercise($e: this,
                            $muscleList: muscleList,
                            difficulty == $difficulty,
                            equipment == $equipment,
                            exerciseCategory == ExerciseCategory.WEIGHT_LOSS || exerciseCategory == ExerciseCategory.ALL
                            ) from $exercises and
                            Number(intValue == 0) from accumulate(
                                Muscle($m: this, $muscleList.contains(this)// this memberOf $muscleList
                                ) from $injuries,
                                count(1)
                            ),
                   $filteredExercises: collectList($e)
        )
	then
	    System.out.println("[TRAINING] Second layer - goal is weight loss, filtering the list of exercises and setting daysweek and rest time to 0");
        $currentFact.setExerciseList($filteredExercises);
        $currentFact.setDaysWeek(0);
        $currentFact.setRestTime(0);
        update($currentFact);
	end
